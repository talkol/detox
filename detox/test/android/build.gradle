buildscript {
    apply from: './rninfo.gradle'

    def androidGradlePluginVersion = rnInfo.isRN62OrHigher ? '3.5.3' : '3.4.2'
    println "[$project] Resorted to Android Gradle-plugin version $androidGradlePluginVersion"

    ext {
        isOfficialDetoxApp = true
        kotlinVersion = '1.3.41'
        buildToolsVersion = '29.0.3'
        compileSdkVersion = 29
        targetSdkVersion = 29
        minSdkVersion = 18
    }
    ext.detoxKotlinVersion = ext.kotlinVersion

    repositories {
        google()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
        classpath 'de.undercouch:gradle-download-task:4.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        // Needed by Spek (https://spekframework.org/setup-android)
        // Here in order to enable unit-tests run from Android Studio when working on the test app.
        classpath 'de.mannodermaus.gradle.plugins:android-junit5:1.4.2.1'
    }
}

allprojects {
    repositories {
        google()
        mavenLocal()
        jcenter()
        maven {
            url "$projectDir/../../node_modules/react-native/android"
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
    }
}

subprojects {
    afterEvaluate { p ->
        if (p.hasProperty('android')){
            android {
                buildToolsVersion rootProject.ext.buildToolsVersion
            }
        }
    }
}
